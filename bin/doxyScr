/^License/,/\*\//{

/^License/,/MA 02110-1301 USA/{

s?^License.*?\*\/\
\/\*! \\file HfilePath\
<b>View </b> <a href="HfilePath"> HfileName </a>\
\\par License\
\\verbatim?

s/MA 02110-1301 USA/MA 02110-1301 USA\
\\endverbatim/

s/^    //
}

/^Namespace/{
N
N
d
}

/^Primitive/{
N
N
d
}

/^Class/{
N
N
d
}

/^Implementation/{
N
N
d
}

/^Application/{
N
N
d
}

/^Type/{
N
N
d
}

/^Global/{
N
N
d
}

/^Description/,/^[^ ]/{
/^Description/c\
\\par Description\
\\verbatim
    /^[^ ]/i\
\\endverbatim\

s/^    //
}

/^Usage/,/^[^ ]/{
/^Usage/c\
\\par Usage\
\\verbatim
    /^[^ ]/i\
\\endverbatim\

s/^    //
}

/SourceFiles/,/^[ ]*$/{
s?SourceFiles?\\par Source files\
    <ul>?
s?^[ ]*$?    </ul>\
?
s? *\([a-zA-Z0-9]*\.[a-zA-Z]*\)?        <li><a href="foamPath/\1"> \1 </a></li>?
}

s/.*\*\//\*\//

}
